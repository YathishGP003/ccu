apply plugin: 'com.android.application'
apply plugin: 'io.objectbox'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

   lintOptions {
        checkReleaseBuilds true
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file("debug.keystore")
            storePassword 'android'
        }

        release {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file("debug.keystore")
            storePassword 'android'
        }
    }

    Properties properties = new Properties()
    if (project.rootProject.file("local.properties").exists()) {
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
    }

    defaultConfig {
        applicationId "a75f.io.renatus"
        minSdkVersion 24
        targetSdkVersion 26
        versionCode getVersionCodeTimestamp()
        versionName applicationName
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        multiDexKeepProguard file('multidex-config.pro')
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
        buildConfigField "String", 'DEBUG_USER', properties.getProperty("debug.user.email", '\"\"')

        signingConfig signingConfigs.debug
    }

    flavorDimensions "mode"

    buildTypes {
        def versionSuffix = VERSION_CODE_MAJOR + "." + VERSION_CODE_MINOR + "." + VERSION_CODE_PATCH

        local {
            minifyEnabled false
            signingConfig signingConfigs.debug
            buildConfigField "String", 'GOOGLE_MAPS_KEY', '\"AIzaSyA867irnfUuznLLsTVFwLs_c-0DNqiVEEY\"'
            versionNameSuffix "_" + 'local' + "_" + versionSuffix
            debuggable true
        }
        dev {
            minifyEnabled false
            signingConfig signingConfigs.debug
            buildConfigField "String", 'GOOGLE_MAPS_KEY', '\"AIzaSyA867irnfUuznLLsTVFwLs_c-0DNqiVEEY\"'
            versionNameSuffix "_" + 'dev' + "_" + versionSuffix
            debuggable true
        }
        prod {
            minifyEnabled false
            signingConfig signingConfigs.release
            ext.betaDistributionGroupAliases = "nightly-1"
            ext.betaDistributionReleaseNotesFilePath="${projectDir}/../CHANGES"
            buildConfigField "String", 'GOOGLE_MAPS_KEY', '\"AIzaSyDEI5fuYQmggzyoDc6cBLciIx3ygY-lmOE\"'
            versionNameSuffix "_" + 'prod' + "_" + versionSuffix
        }
        qa {
            minifyEnabled false
            signingConfig signingConfigs.debug
            buildConfigField "String", 'GOOGLE_MAPS_KEY', '\"AIzaSyCo7HhoO6m40JWyosWl3RqMjTZd5GI0znU\"'
            versionNameSuffix "_" + 'qa' + "_" + versionSuffix
        }
        staging {
            minifyEnabled false
            signingConfig signingConfigs.debug
            buildConfigField "String", 'GOOGLE_MAPS_KEY', '\"AIzaSyA-b5lbhs7XOO8Fy1bdNky7CoNJR4B3kyY\"'
            versionNameSuffix "_" + 'staging' + "_" + versionSuffix
        }
    }

    android.variantFilter { variant ->
        if(variant.buildType.name.endsWith('release') || variant.buildType.name.endsWith('debug')) {
            variant.setIgnore(true);
        }
    }

    configurations {
        compile.exclude group:'org.slf4j'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'com/fasterxml/jackson/core/json/VERSION.txt'
        exclude 'com/fasterxml/jackson/core/Base64Variant'
        exclude 'com/google/api/client/http/AbstractHttpContent.class'
        exclude 'thirdpartynotice.txt'
        exclude 'simplelogger.properties'
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }

    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets','src/androidTest/assets']
            java {
                srcDirs = ['build/generated/source', 'src/main/java']
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    useLibrary 'org.apache.http.legacy'
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation project(':logic')
    implementation project(':device')
    implementation project(':usbserial')
    implementation 'org.greenrobot:eventbus:3.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'com.jakewharton:butterknife:10.2.3'
    implementation 'androidx.fragment:fragment:1.2.4'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.0'

    implementation project(path: ':modbus4j')
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.annotation:annotation:1.0.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'

    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.0'

    // crash reporting
    implementation 'com.raygun:raygun4android:4.+'

    implementation 'com.opencsv:opencsv:4.0'
    implementation 'org.jsoup:jsoup:1.10.3'
    implementation project(':algos')
    implementation project(':alerts')
    implementation project(path: ':modbusbox')
    implementation project(path: ':haystack')
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation('com.github.prolificinteractive:material-calendarview:2.0.1') {
        transitive = true
        exclude group: 'org.threeten'
    }
    implementation 'org.threeten:threetenbp:1.4.0'
    implementation project(':BACnet4J')
    implementation files('libs/slf4j-api-1.7.28.jar')
}



def getVersionCodeTimestamp() {
    def currentDate = new Date()
    def formattedDate = currentDate.format('yyMMddHHmm')
    def code = formattedDate.toInteger()
    return code
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        if (variant.buildType.name == "local") {
            outputFileName = "app-" + variant.buildType.name + "-debug.apk"
        } else {
            outputFileName = versionName + ".apk"
        }
    }
}

