plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk rootProject.ext.compileSdkVersion


    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        /*javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }*/
    }

    buildTypes {
        local {
            minifyEnabled false
        }
        dev {
            minifyEnabled false
        }
        dev_qa {
            minifyEnabled false
        }
        prod {
            minifyEnabled false
            ext.betaDistributionGroupAliases = "nightly-1"
            ext.betaDistributionReleaseNotesFilePath="${projectDir}/../CHANGES"
        }
        qa {
            minifyEnabled false
        }
        carrier_prod {
            minifyEnabled false
            ext.betaDistributionGroupAliases = "nightly-1"
            ext.betaDistributionReleaseNotesFilePath="${projectDir}/../CHANGES"
        }
        staging {
            minifyEnabled false
        }
        daikin_prod{
            minifyEnabled false
            ext.betaDistributionGroupAliases = "nightly-1"
            ext.betaDistributionReleaseNotesFilePath="${projectDir}/../CHANGES"
        }
        airoverse_prod {
            minifyEnabled false
            ext.betaDistributionGroupAliases = "nightly-1"
            ext.betaDistributionReleaseNotesFilePath="${projectDir}/../CHANGES"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    sourceSets {
        main.java.srcDirs += "$buildDir/generated-src"
    }
}

configurations {
    antlr
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.7.0')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.7.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation "org.antlr:antlr4-runtime:4.13.1"
    antlr "org.antlr:antlr4:4.13.1"
}

task generateGrammarSource(type: JavaExec) {
    def outputDir = "$buildDir/generated-src"
    inputs.files fileTree(dir: "src/main/antlr", include: "**/*.g4")
    outputs.dir outputDir

    doFirst {
        file(outputDir).mkdirs()
    }

    classpath = configurations.antlr
    main = 'org.antlr.v4.Tool'
    args = [
            "-visitor",
            "-long-messages",
            "-o", outputDir,
            "src/main/antlr/CcuQuery.g4"
    ]
}

preBuild.dependsOn generateGrammarSource